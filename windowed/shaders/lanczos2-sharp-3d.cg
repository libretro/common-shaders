#pragma parameter LANCZOS2_WINDOW_SINC "Window Sinc Param" 0.5 0.0 1.0 0.01
#pragma parameter LANCZOS2_SINC "Sinc Param" 1.0 0.0 1.0 0.01
#pragma parameter LANCZOS2_AR_STRENGTH "Anti-ringing Strength" 0.8 0.0 1.0 0.1
#pragma parameter LANCZOS2_RES "Internal Res Multiplier" 2.0 1.0 7.0 1.0
#ifdef PARAMETER_UNIFORM
uniform float LANCZOS2_WINDOW_SINC;
uniform float LANCZOS2_SINC;
uniform float LANCZOS2_AR_STRENGTH;
uniform float LANCZOS2_RES;
#else
#define LANCZOS2_WINDOW_SINC 0.5
#define LANCZOS2_SINC 1.0
#define LANCZOS2_AR_STRENGTH 0.8
#define LANCZOS2_RES 2.0
#endif
// END PARAMETERS //

/* COMPATIBILITY 
   - HLSL compilers
   - Cg   compilers
   - FX11 compilers
*/

/*
   Hyllian's lanczos windowed-jinc 2-lobe sharper 3D with anti-ringing Shader
   
   Copyright (C) 2011/2016 Hyllian - sergiogdb@gmail.com

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is 
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in
   all copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
   THE SOFTWARE.

*/

#include "../../compat_includes.inc"
uniform COMPAT_Texture2D(decal) : TEXUNIT0;
uniform float4x4 modelViewProj;

struct out_vertex
{
	float4 position : COMPAT_POS;
	float2 texCoord : TEXCOORD0;
#ifndef HLSL_4
	float4 Color    : COLOR;
#endif
};

out_vertex main_vertex(COMPAT_IN_VERTEX)
{
	out_vertex OUT;
#ifdef HLSL_4
	float4 position = VIN.position;
	float2 texCoord = VIN.texCoord;
#else
	OUT.Color = color;
#endif
	OUT.position = mul(modelViewProj, position);
	OUT.texCoord = texCoord;
	
	return OUT;
}

	/*
	   This is an approximation of Jinc(x)*Jinc(x*r1/r2) for x < 2.5,
	   where r1 and r2 are the first two zeros of jinc function.
	   For a jinc 2-lobe best approximation, use A=0.5 and B=0.825.
	*/  

// A=0.5, B=0.825 is the best jinc approximation for x<2.5. if B=1.0, it's a lanczos filter.
// Increase A to get more blur. Decrease it to get a sharper picture. 
// B = 0.825 to get rid of dithering. Increase B to get a fine sharpness, though dithering returns.

#define halfpi  1.5707963267948966192313216916398
#define pi	3.1415926535897932384626433832795
#define wa	(LANCZOS2_WINDOW_SINC*pi)
#define wb	(LANCZOS2_SINC*pi)

const static float3 dtt = float3(65536,255,1);

float4 reduce4(float3 A, float3 B, float3 C, float3 D)
{
  return mul(float4x3(A, B, C, D), dtt);
}

// Calculates the distance between two points
float d(float2 pt1, float2 pt2)
{
  float2 v = pt2 - pt1;
  return sqrt(dot(v,v));
}

float3 min4(float3 a, float3 b, float3 c, float3 d)
{
	return min(a, min(b, min(c, d)));
}

float3 max4(float3 a, float3 b, float3 c, float3 d)
{
	return max(a, max(b, max(c, d)));
}
 
float4 resampler(float4 x)
{
	float4 res;

	res = (x==float4(0.0, 0.0, 0.0, 0.0)) ?  float4(wa*wb,wa*wb,wa*wb,wa*wb)  :  sin(x*wa)*sin(x*wb)/(x*x);

	return res;
}
	 
float4 lanczos2_sharp_3D(float2 texture_size, float2 texCoord, COMPAT_Texture2D(decal))
{
	float3 color, E;
	float4x4 weights;

	float2 dx = float2(1.0, 0.0);
	float2 dy = float2(0.0, 1.0);

	float2 pc = texCoord*texture_size/LANCZOS2_RES;
	float2 tex = (floor(pc) + float2(0.5, 0.5))*LANCZOS2_RES/texture_size;

	float2 tc = (floor(pc-float2(0.5,0.5))+float2(0.5,0.5));
	 
	weights[0] = resampler(float4(d(pc, tc	-dx	-dy), d(pc, tc		 -dy), d(pc, tc	+dx	-dy), d(pc, tc+2.0*dx	-dy)));
	weights[1] = resampler(float4(d(pc, tc	-dx	 ), d(pc, tc		  ), d(pc, tc	+dx	 ), d(pc, tc+2.0*dx	 )));
	weights[2] = resampler(float4(d(pc, tc	-dx	+dy), d(pc, tc		 +dy), d(pc, tc	+dx	+dy), d(pc, tc+2.0*dx	+dy)));
	weights[3] = resampler(float4(d(pc, tc	-dx+2.0*dy), d(pc, tc	 +2.0*dy), d(pc, tc	+dx+2.0*dy), d(pc, tc+2.0*dx+2.0*dy)));

	dx = dx*LANCZOS2_RES/texture_size;
	dy = dy*LANCZOS2_RES/texture_size;
	tc = tc*LANCZOS2_RES/texture_size;
	 
	// reading the texels
	 
	float3 c00 = COMPAT_SamplePoint(decal, tc	-dx	-dy).xyz;
	float3 c10 = COMPAT_SamplePoint(decal, tc		 -dy).xyz;
	float3 c20 = COMPAT_SamplePoint(decal, tc	+dx	-dy).xyz;
	float3 c30 = COMPAT_SamplePoint(decal, tc+2.0*dx	-dy).xyz;
	float3 c01 = COMPAT_SamplePoint(decal, tc	-dx	 ).xyz;
	float3 c11 = COMPAT_SamplePoint(decal, tc		  ).xyz;
	float3 c21 = COMPAT_SamplePoint(decal, tc	+dx	 ).xyz;
	float3 c31 = COMPAT_SamplePoint(decal, tc+2.0*dx	 ).xyz;
	float3 c02 = COMPAT_SamplePoint(decal, tc	-dx	+dy).xyz;
	float3 c12 = COMPAT_SamplePoint(decal, tc		 +dy).xyz;
	float3 c22 = COMPAT_SamplePoint(decal, tc	+dx	+dy).xyz;
	float3 c32 = COMPAT_SamplePoint(decal, tc+2.0*dx	+dy).xyz;
	float3 c03 = COMPAT_SamplePoint(decal, tc	-dx+2.0*dy).xyz;
	float3 c13 = COMPAT_SamplePoint(decal, tc	 +2.0*dy).xyz;
	float3 c23 = COMPAT_SamplePoint(decal, tc	+dx+2.0*dy).xyz;
	float3 c33 = COMPAT_SamplePoint(decal, tc+2.0*dx+2.0*dy).xyz;

	color = E = COMPAT_SamplePoint(decal, texCoord).xyz;

	float3 F6 = COMPAT_SamplePoint(decal, tex +dx+0.25*dx+0.25*dy).xyz;
	float3 F7 = COMPAT_SamplePoint(decal, tex +dx+0.25*dx-0.25*dy).xyz;
	float3 F8 = COMPAT_SamplePoint(decal, tex +dx-0.25*dx-0.25*dy).xyz;
	float3 F9 = COMPAT_SamplePoint(decal, tex +dx-0.25*dx+0.25*dy).xyz;

	float3 H6 = COMPAT_SamplePoint(decal, tex +0.25*dx+0.25*dy+dy).xyz;
	float3 H7 = COMPAT_SamplePoint(decal, tex +0.25*dx-0.25*dy+dy).xyz;
	float3 H8 = COMPAT_SamplePoint(decal, tex -0.25*dx-0.25*dy+dy).xyz;
	float3 H9 = COMPAT_SamplePoint(decal, tex -0.25*dx+0.25*dy+dy).xyz;

	float4 f0 = reduce4(F6, F7, F8, F9);
	float4 h0 = reduce4(H6, H7, H8, H9);

	bool block_3d = (all(f0.xyz==f0.yzw) && all(h0.xyz==h0.yzw));

	//  Get min/max samples
	float3 min_sample = min4(c11, c21, c12, c22);
	float3 max_sample = max4(c11, c21, c12, c22);

	color = mul(weights[0], float4x3(c00, c10, c20, c30));
	color+= mul(weights[1], float4x3(c01, c11, c21, c31));
	color+= mul(weights[2], float4x3(c02, c12, c22, c32));
	color+= mul(weights[3], float4x3(c03, c13, c23, c33));
	color = color/(dot(mul(weights, float4(1,1,1,1)), 1));

	// Anti-ringing
	float3 aux = color;
	color = clamp(color, min_sample, max_sample);

	color = lerp(aux, color, LANCZOS2_AR_STRENGTH);
 
	color = block_3d ? color : E;

	// final sum and weight normalization
	return float4(color, 1);
	 
	}

float4 main_fragment(COMPAT_IN_FRAGMENT) : COMPAT_Output
{
	return lanczos2_sharp_3D(COMPAT_texture_size, VOUT.texCoord, decal);
}
COMPAT_END